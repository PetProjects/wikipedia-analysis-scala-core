create database if not exists wikitrending
    comment 'i.dobrovolskiy hive homework 1 db'
    location '/hdfs/user/idobrovolskiy/wikipedia-trending/hivedb'
    with dbproperties ('creator'='i.dobrovolskiy','date'='16.08.2017');
	
use wikitrending;

drop table data_range;

CREATE EXTERNAL TABLE IF NOT EXISTS data_range (
  wiki_id int,
  wiki_title string,
  wiki_url string,
  dates struct<min_date: bigint, max_date: bigint, date_range: array<struct<since: bigint, until: bigint>>>,
  top_tokens map<string, int>
)
STORED AS PARQUET
LOCATION '/user/idobrovolskiy/wikipedia-trending/wiki_index_date_ranges';

drop table data_range_hv;

CREATE EXTERNAL TABLE IF NOT EXISTS data_range_hv (
  wiki_id int,
  wiki_title string,
  wiki_url string,
  min_date bigint,
  max_date bigint,
  date_range array<struct<since: bigint, until: bigint>>,
  top_tokens map<string, int>
)
CLUSTERED BY (min_date) SORTED BY (max_date) INTO 25 BUCKETS
STORED AS ORC
LOCATION '/user/idobrovolskiy/wikipedia-trending/date_ranges.hive'
TBLPROPERTIES ( 
'orc.compress'='SNAPPY',
'orc.create.index'='true',
'orc.bloom.filter.columns'='min_date, max_date'
--'orc.bloom.filter.fpp'='0.05',
--'orc.stripe.size'='268435456',
--'orc.row.index.stride'='10000'
);

INSERT OVERWRITE TABLE data_range_hv
 SELECT wiki_id, wiki_title, wiki_url, dates.min_date, dates.max_date, dates.date_range, top_tokens
 FROM data_range;
 
 select
    tt.token, sum(tt.tcount) as total_tcount
from (
    select t.top_tokens
    from data_range_hv t
    lateral view inline(t.date_range) drs as since, until
    where t.min_date <= -701265 and t.max_date >= -737790 and drs.since <=-701265 and drs.until >= -737790
) t
lateral view explode(t.top_tokens) tt as token, tcount
group by tt.token
order by total_tcount desc
limit 10;
